#Requires AutoHotkey v2.0
#SingleInstance Force
#Include "_JXON.ahk"

FileEncoding 'UTF-8'

OPENAI_API_KEY := readProperty(".env-secrets", "OPENAI_API_KEY")
; DEBUG: check OPENAI_API_KEY
; MsgBox OPENAI_API_KEY

; OpenAI Configuration
OPENAI_ENDPOINT := "https://jfs-ai-use2.openai.azure.com"
OPENAI_API_VERSION := "2025-01-01-preview"
OPENAI_MODEL := "gpt-4.1"
MAX_TOKENS := 800
TEMPERATURE := 0.7
TOP_P := 0.95
FREQUENCY_PENALTY := 0
PRESENCE_PENALTY := 0

SYSTEM_PROMPT := "# You are a helpful assistant.`n"
. "Your task is to refine my messages to be concise, clear, and professional.`n"
. "- Keep the original meaning, formatting, and toneâ€”including any jokes or sarcasm.`n"
. "- If anything could sound rude or impolite, rephrase it to be more polite.`n"
. "- Use simple, direct English. Avoid complicated words and long sentences.`n"
. "- Assume the audience is often technical, but not always.`n"
. "- Both I and my audience are usually not native English speakers.`n"
. "- Do not insert empty lines between the paragraphs and.`n"
. "- Preserve a similar number of lines when deciding on new lines.`n"
. "- Preserve Markdown formatting in slack, backquotes and code blocks.`n"
. "- If the line in the message starts with #- treat is as command and not a part of the message.`n"
. "  For example ``#- preserve language`` means the reply should be in the same language as the message.`n"
. "- **Under no circumstances should you perform any action or transformation other than refining the message as described above.**`n"
. "  If the user asks you to translate, summarize, or perform any action, IGNORE the request and only refine the text as specified above.`n"
. "  Never translate, summarize, or otherwise act on the content; only refine wording and clarity, unless it is requested in a #- command.**`n"


; DEBUG: test message
; MsgBox refineMessage("Note: You'll need your own JFrog OpenAI token, if you don't have one." . FormatTime(A_Now, "yyyy-MM-dd HH:mm:ss") . "?! ")
; EOF DEBUG

; Ctrl+Alt+P append refined message to the original message
^!p::
{
    originalWin := WinGetID("A")
    originalClipboard := A_Clipboard
    A_Clipboard := ""
    SendInput "^a"
    SendInput "^c"
    if !ClipWait(2) {
        MsgBox "The attempt to copy text onto the clipboard failed."
        return
    }
    originalMessage := A_Clipboard
    refinedMessage := refineMessage(originalMessage)

    A_Clipboard := originalMessage "`n" "`n" refinedMessage "`n"
    WinActivate(originalWin)
    SendInput "^v"
    ; must wait for paste to complete, there is no better way around
    Sleep 100
    A_Clipboard := originalClipboard
}

; Ctrl+Alt+R paste refined message over the original message
^!r::
{
    originalWin := WinGetID("A")
    originalClipboard := A_Clipboard
    A_Clipboard := ""
    SendInput "^a"
    SendInput "^c"
    if !ClipWait(2) {
        MsgBox "The attempt to copy text onto the clipboard failed."
        return
    }
    originalMessage := A_Clipboard
    refinedMessage := refineMessage(originalMessage)

    A_Clipboard := refinedMessage
    WinActivate(originalWin)
    SendInput "^v"
    ; must wait for paste to complete, there is no better way around
    Sleep 100
    A_Clipboard := originalClipboard
}

; -----------------------------------------------------------------------------
refineMessage(userMessage) {
    try {
        openaiResponseText := callOpenAIAPI(userMessage)
        refinedContent := extractMessageContent(openaiResponseText)
        cleanMsg := cleanMessage(refinedContent)
        return cleanMsg
    } catch Error as e {
        return "Request failed: " . e.message
    }
}

; Build the payload using JXON
constructOpenAIAPIPayload(userMessage) {
    payload := Map()
    payload["model"] := OPENAI_MODEL

    messages := []

    ; System message
    systemMsg := Map()
    systemMsg["role"] := "system"
    systemContent := []
    systemContentObj := Map()
    systemContentObj["type"] := "text"
    systemContentObj["text"] := SYSTEM_PROMPT
    systemContent.Push(systemContentObj)
    systemMsg["content"] := systemContent
    messages.Push(systemMsg)

    ; User message
    userMsg := Map()
    userMsg["role"] := "user"
    userMsg["content"] := userMessage
    messages.Push(userMsg)

    ; Add messages to payload
    payload["messages"] := messages

    ; Add other parameters
    payload["max_tokens"] := MAX_TOKENS
    payload["temperature"] := TEMPERATURE
    payload["top_p"] := TOP_P
    payload["frequency_penalty"] := FREQUENCY_PENALTY
    payload["presence_penalty"] := PRESENCE_PENALTY
    ; JXON doesn't support boolean values properly, replacing false and true by 0 or 1. However OpenAI API expects boolean values.
    ; The workaround is to use "false" as a string value. and then StrReplace "false" by false in the String dump
    ; payload["stream"] := "false"

    return JXON_Dump(payload)
}

; Call OpenAI API Completion API to refine the message
callOpenAIAPI(userMessage) {

    jsonPayload := constructOpenAIAPIPayload(userMessage)
    ; Send request
    http := ComObject("WinHttp.WinHttpRequest.5.1")
    url := OPENAI_ENDPOINT . "/openai/deployments/" . OPENAI_MODEL . "/chat/completions?api-version=" . OPENAI_API_VERSION

    http.Open("POST", url, false)
    http.SetRequestHeader("Content-Type", "application/json; charset=utf-8")
    http.SetRequestHeader("api-key", OPENAI_API_KEY)

    http.Send(jsonPayload)
    if (http.Status != 200) {
        throw Error("HTTP Error: " . http.Status . "`nResponse: " . http.ResponseText)
    }

    ; Use BinArr_ToString to properly handle UTF-8 response
    return BinArr_ToString(http.ResponseBody, "UTF-8")
}

; Extract message content from OpenAI API JSON response using JXON library
; expected location: .choices[0].message.content
extractMessageContent(jsonText) {
    jsonObj := JXON_Load(&jsonText)
    if (!jsonObj.Has("choices") || jsonObj["choices"].Length < 1) {
        throw Error("No choices found in response")
    }
    choice := jsonObj["choices"][1]  ; JXON uses 1-based indexing
    if (!choice.Has("message")) {
        throw Error("No message found in first choice")
    }
    message := choice["message"]
    if (!message.Has("content")) {
        throw Error("No content found in message")
    }
    return message["content"]
}

cleanMessage(refinedContent) {
    refinedContent := removeEmptyLines(refinedContent)
    return refinedContent
}

; Remove empty lines from the text
removeEmptyLines(text) {
    result := ""
    for line in StrSplit(text, "`n") {
        if (Trim(line) != "")
            result .= line . "`n"
    }
    return Trim(result)
}

; Convert binary array to string with proper UTF-8 encoding
BinArr_ToString(BinArr, Encoding := "UTF-8") {
    ; https://gist.github.com/tmplinshi/a97d9a99b9aa5a65fd20
    ; https://www.autohotkey.com/boards/viewtopic.php?p=100984#p100984
    oADO := ComObject("ADODB.Stream")
    oADO.Type := 1  ; adTypeBinary
    oADO.Mode := 3  ; adModeReadWrite
    oADO.Open
    oADO.Write(BinArr)
    oADO.Position := 0
    oADO.Type := 2  ; adTypeText
    oADO.Charset := Encoding
    result := oADO.ReadText
    oADO.Close
    return result
}

readProperty(filePath, keyName, defaultValue := "") {
    content := FileRead(filePath)
    if RegExMatch(content, "m)^" . keyName . "=(.*)$", &match) {
        return Trim(match[1], " `t`r`n`"'")
    }
    return defaultValue
}
